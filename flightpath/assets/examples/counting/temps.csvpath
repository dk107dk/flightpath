---- CSVPATH ----

~
  This csvpath finds the average temp for all days sampled in each month. if the
  average is too high or low it registers an error.
  id: average temps
  validation-mode:print, no-raise
  test-data: examples/counting/temps.txt
~
$[1*][

   ~ find the month number ~
   @month = month(#0)

   ~ get a subtotal of the month's temp samples ~
   @sub = subtotal(@month, #temp)

   ~ count the number of samples for the month ~
   tally.obs(@month)

   ~ get the count of observations in month ~
   @obs = get("obs_month", @month)

   ~ get the average for the month ~
   @ave = divide(@sub, @obs)

   ~ print running average in a table just for checking our work ~
   put("Temperature Data", "Observation", #0)
   put("Temperature Data", "Temp", #temp)
   put("Temperature Data", "Number of observations in month", @obs)
   put("Temperature Data", "Average", format(@ave, ".2f"))
   var_table("Temperature Data")

   ~ the validation rule ~
   above(@ave, 40) -> error("Average temp in $.variables.month is too high: $.variables.ave")
   below(@ave, 30) -> error("Average temp in $.variables.month is too low: $.variables.ave ")
]
